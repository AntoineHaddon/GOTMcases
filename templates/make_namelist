#!/usr/bin/tcsh

#$Id: make_namelist,v 1.1.1.1 2003/03/11 13:38:58 kbk Exp $

set case 	= $1

if( $# == 2 ) then
   set outdir = $2
else
   set outdir = .
endif

if ( ! $?GOTM_CASES ) then
  setenv GOTM_CASES $HOME/gotm-cases/v4.2
endif

if ( ! -d $GOTM_CASES ) then
   echo ""
   echo "'$GOTM_CASES' does not point to an existing directory"
   echo "You  must set 'GOTM_CASES' to e.g $HOME/gotm-cases/v4.0"
   echo ""
   exit
endif
echo "Using templates from: $GOTM_CASES"

set template_dir	= $GOTM_CASES/templates

#set NML_CORE = ( airsea gotmmean gotmrun gotmturb obs seagrass kpp )
set NML_CORE = ( airsea gotmmean gotmrun gotmturb obs kpp )

echo ""

# loop over all core  .nml files
foreach f ( $NML_CORE )
   set proto  = $template_dir/$f.proto
   set values = $case.values
   set out    = $outdir/$f.nml
   if ( ! -e $values ) then
      echo "Creating new value file"
      cp $template_dir/proto.values $case.values
   endif
   if( -r $proto && -r $values ) then 
      echo "Processing:  "$proto
      echo "Values from: "$values
      echo "Output to:   "$out
      cat $proto | sed -f $values > $out
   endif
end

# loop over any extra .nml files
if ( $?NML_EXTRA ) then
echo $NML_EXTRA
foreach f ( $NML_EXTRA )
   set proto  = $template_dir/$f.proto
   set values = $f.values
   set out    = $outdir/$f.nml
   if ( ! -e $values ) then
      echo "Creating new value file"
      cp $template_dir/proto.values $case.values
   endif
   if( -r $proto && -r $values ) then 
      echo "Processing:  "$proto
      echo "Values from: "$values
      echo "Output to:   "$out
      cat $proto | sed -f $values > $out
   endif
end
endif

echo "Now run the 'gotm' executable"

